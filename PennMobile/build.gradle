plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.kotlin.android)
    alias(libs.plugins.google.services)
    alias(libs.plugins.firebase.crashlytics)
    alias(libs.plugins.ktlint)
    // Removed problematic compose compiler alias for Kotlin 1.9.x compatibility
    id 'jacoco'
    id 'kotlin-parcelize'
    id 'com.google.devtools.ksp'
    id 'com.google.dagger.hilt.android'
}

android {
    namespace = 'com.pennapps.labs.pennmobile'

    buildFeatures {
        buildConfig = true
        viewBinding = true
        compose = true
    }

    buildTypes {
        debug {
            matchingFallbacks = ['qa', 'release']
            enableUnitTestCoverage = true
            enableAndroidTestCoverage = true
        }
        release {}
    }

    compileSdk = 35

    composeOptions {
        // Matches Kotlin 1.9.24 defined in your libs.versions.toml
        kotlinCompilerExtensionVersion = "1.5.14"
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    defaultConfig {
        minSdkVersion = 26
        targetSdkVersion = 35
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled = true
        buildConfigField ("String", "PLATFORM_REDIRECT_URI", getPlatformRedirectUri())
        buildConfigField ("String", "PLATFORM_CLIENT_ID", getPlatformClientID())
    }

    packagingOptions {
        resources {
            excludes += ['META-INF/rxjava.properties']
            pickFirsts += ['META-INF/LICENSE.txt', 'META-INF/NOTICE.txt']
        }
    }
}

ktlint {
    version.set("1.8.0")
    android.set(true)
    ignoreFailures.set(false)
    verbose.set(true)
    outputToConsole.set(true)
    coloredOutput.set(true)

    filter {
        exclude("**/build/**")
        exclude("**/generated/**")
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
// If you want the foundation layout, use the bundle or the specific activity compose you have defined
    implementation libs.androidx.activity.compose
    implementation libs.androidx.material3.android
//    implementation libs.androidx.foundation.layout

    ktlintRuleset libs.ktlint

    androidTestImplementation libs.androidx.espresso.core
    androidTestImplementation libs.androidx.junit
    androidTestImplementation libs.kaspresso
    androidTestImplementation libs.testng

    annotationProcessor libs.compiler

    debugImplementation libs.leakcanary.android

    implementation libs.androidx.annotation
    implementation (libs.androidx.lifecycle.extensions) {
        transitive = true
    }
    implementation libs.androidx.browser
    implementation libs.androidx.cardview
    implementation libs.androidx.constraintlayout
    implementation libs.androidx.coordinatorlayout
    implementation libs.androidx.exifinterface
    implementation libs.androidx.legacy.support.v4
    implementation libs.androidx.lifecycle.runtime.ktx
    implementation libs.androidx.multidex
    implementation libs.androidx.palette.ktx
    implementation libs.androidx.percentlayout
    implementation libs.androidx.preference.ktx
    implementation libs.androidx.recyclerview
    implementation libs.lottie
    implementation libs.library
    implementation libs.blurview
    implementation libs.mpandroidchart
    implementation libs.android.rss
    implementation libs.glide
    implementation(libs.customalertviewdialogue){
        exclude group: 'com.github.Dimezis.BlurView', module: 'blurview'
    }
    implementation libs.logging.interceptor
    implementation libs.okhttp
    implementation libs.picasso
    implementation libs.adapter.rxjava
    implementation libs.reactivex.rxandroid
    implementation libs.rxjava
    implementation libs.joda.time
    implementation libs.commons.lang3
    implementation libs.kotlinx.coroutines.core
    implementation libs.jsoup
    implementation platform(libs.androidx.compose.bom)
    testImplementation platform(libs.androidx.compose.bom)
    androidTestImplementation platform(libs.androidx.compose.bom)
    implementation platform(libs.firebase.bom)

    implementation libs.bundles.compose
    implementation libs.bundles.material
    implementation libs.bundles.firebase
    implementation libs.bundles.runtime
    implementation libs.bundles.retrofit2
    implementation libs.bundles.ui
    implementation libs.bundles.google
    implementation libs.bundles.hilt

    ksp libs.androidx.room.compiler
    ksp libs.androidx.hilt.compiler

    implementation libs.androidx.constraintlayout
    implementation libs.androidx.hilt.navigation.compose

    implementation libs.androidx.hilt.android
    ksp libs.androidx.hilt.compiler
}

String getPlatformClientID() {
    def propFile = rootProject.file("./local.properties")
    def properties = new Properties()
    if (propFile.exists()) {
        properties.load(new FileInputStream(propFile))
        return properties['PLATFORM_CLIENT_ID']
    }
    return ""
}

String getPlatformRedirectUri() {
    def propFile = rootProject.file("./local.properties")
    def properties = new Properties()
    if (propFile.exists()) {
        properties.load(new FileInputStream(propFile))
        return properties['PLATFORM_REDIRECT_URI']
    }
    return ""
}

jacoco {
    toolVersion = "0.8.11"
}

tasks.withType(Test).configureEach {
    jacoco.includeNoLocationClasses = true
    jacoco.excludes = ['jdk.internal.*']
}

def fileFilter = ['**/R.class', '**/R$*.class', '**/BuildConfig.*',
                  '**/Manifest*.*', '**/*Test*.*', 'android/**/*.*']

def debugTree = fileTree(dir: "$project.layout.buildDirectory/tmp/kotlin-classes/debug",
        excludes: fileFilter)

def mainSrc = "$project.projectDir/src/main/java"

tasks.register('jacocoTestReport', JacocoReport) {
    dependsOn = ['testDebugUnitTest', 'createDebugCoverageReport']

    sourceDirectories.setFrom(files([mainSrc]))
    classDirectories.setFrom(files([debugTree]))

    executionData.from = fileTree(dir: project.layout.buildDirectory, includes: [
            'jacoco/testDebugUnitTest.exec'
    ])
}